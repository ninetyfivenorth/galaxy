# Author: Julien MISCHKOWITZ <wain@archlinux.fr>
# Author: tuxce <tuxce.net@gmail.com>
# Contributor: Skunnyk <skunnyk@archlinux.fr>

pkgname=yaourt
pkgver=1.9
pkgrel=2
pkgdesc="A pacman wrapper with extended features and AUR support"
arch=('any')
url="https://github.com/archlinuxfr/$pkgname"
license=(GPL)
depends=('diffutils' 'pacman>=5.0' 'package-query>=1.8' 'gettext')
optdepends=('rsync: retrieve PKGBUILD from official repositories')
backup=('etc/yaourtrc')
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz" '0001-alpm-artix-brand.patch')
sha256sums=('9a485cef9d50e80b8abae5dbb147e09bdeb8818d29316b65e892fb560c48517d'
            '429b42a166bb4d8ee43de18d4d6397cf5cc0dc23370300d56832e4e4569b66c6')

prepare() {
    cd "$pkgname-$pkgver"
    patch -Np1 -i "$srcdir"/0001-alpm-artix-brand.patch

    # Don't prompt the user to edit files
    #sed -i "s/\#EDITFILES\=1/EDITFILES\=0/g" yaourtrc.in
    # Save package tarball to pacman cache
    sed -i "s/\#EXPORT\=0/EXPORT\=2/g" yaourtrc.in
}

build() {
    make -C "$pkgname-$pkgver" PREFIX=/usr sysconfdir=/etc localstatedir=/var
}

package() {
    make -C "$pkgname-$pkgver" PREFIX=/usr sysconfdir=/etc localstatedir=/var DESTDIR="$pkgdir" install
}
